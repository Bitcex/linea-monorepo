import org.web3j.codegen.SolidityFunctionWrapperGenerator

buildscript {
  repositories {
    maven { url("https://plugins.gradle.org/m2/") }
  }
  dependencies {
    classpath("org.web3j:codegen:${versions.web3j}")
  }
}

plugins {
  id 'net.consensys.zkevm.kotlin-library-conventions'
}

def abisDirectory =  layout.buildDirectory.dir("${rootProject.projectDir}/contracts/abi").get()
def generatedJavaWrappers = layout.buildDirectory.dir("generated/sources/web3j/main/java").get()

dependencies {
  implementation("org.web3j:core:${versions.web3j}") {
    exclude group: 'org.slf4j', module: 'slf4j-nop'
  }
}

sourceSets {
  main {
    java.srcDir generatedJavaWrappers
  }
}

compileJava {
  source("generateLineaContractWrappers.outputs")
  dependsOn("generateLineaContractWrappers")
}

compileKotlin {
  source("generateLineaContractWrappers.outputs")
  dependsOn("generateLineaContractWrappers")
}

task generateLineaContractWrappers(type: DefaultTask) {
  inputs.dir(abisDirectory)
  outputs.dir(generatedJavaWrappers)
  doFirst {
    abisDirectory.asFile.mkdirs()
    fileTree(abisDirectory).matching {
      include "**/ZkEvmV2.abi", "**/L2MessageService.abi"
    }.each {
      String[] params = [
        "--abiFile",
        it.getAbsolutePath(),
        "--outputDir",
        generatedJavaWrappers,
        "--package",
        "net.consensys.linea.contract"]
      SolidityFunctionWrapperGenerator.main(params)
    }
  }
}

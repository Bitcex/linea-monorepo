plugins {
  id 'net.consensys.zkevm.kotlin-common-minimal-conventions'
  id 'net.consensys.zkevm.linea-native-libs-helper'
  alias(libs.plugins.jreleaser)
}

description = 'Java JNA wrapper for Linea Blob Shnarf Calculator implemented in GO Lang'
apply from: rootProject.file("gradle/publishing.gradle")

dependencies {
  api "net.java.dev.jna:jna:${libs.versions.jna.get()}"
  testImplementation project(":jvm-libs:kotlin-extensions")
}

jar {
  dependsOn configurations.runtimeClasspath
}


test {
  // we cannot have more 1 compressor per JVM, hence we disable parallel execution
  // because multiple threads would cause issues with the native library
  systemProperties["junit.jupiter.execution.parallel.enabled"] = false
  maxParallelForks = 1
}

def libsZipDownloadOutputDir = project.parent.layout.buildDirectory.asFile.get().absolutePath

task downloadNativeLibs {
  doLast {
    fetchLibFromZip("https://github.com/Consensys/linea-monorepo/releases/download/blob-libs-v0.1.0/linea-blob-libs-v0.1.0.zip", "shnarf_calculator", libsZipDownloadOutputDir)
    fetchLibFromZip("https://github.com/Consensys/linea-monorepo/releases/download/blob-libs-v1.0.1/linea-blob-libs-v1.0.1.zip", "shnarf_calculator", libsZipDownloadOutputDir)
  }
}

//  GITHUB_TOKEN=ghp_XXX ./gradlew jvm-libs:blob-compressor:downloadNativeLib -PreleaseTag=blob-libs-v0.1.0 -PlibName=blob_compressor
tasks.register('downloadNativeLib') {
  def releaseTag
  def libName

  doLast {
    if (project.hasProperty("releaseTag")) {
      releaseTag = project.releaseTag
    } else {
      throw new GradleException("releaseTag is required")
    }
    if (project.hasProperty("libName")) {
      libName = project.libName
    } else {
      throw new GradleException("libName is required")
    }

    fetchLib(
      releaseTag,
      libName,
      libsZipDownloadOutputDir
    )
  }
}

compileKotlin {
  dependsOn tasks.downloadNativeLibs
}
